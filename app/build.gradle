apply plugin: 'com.android.application'

repositories {
    maven {
        url 'https://maven.google.com'
    }
}

ext {
    supportLibVersion = '26.1.0'
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.3"


    defaultConfig {
        applicationId "remix.myplayer"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 52
        versionName "1.2.6.1"
        flavorDimensions "default"
        vectorDrawables.useSupportLibrary = true
        ndk {
            abiFilters 'armeabi-v7a','x86'
        }
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
        multiDexEnabled true
    }

    signingConfigs{
        debug {
            storeFile file("/../Debug.jks")
            storePassword "123456"
            keyAlias "Debug"
            keyPassword "123456"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
        }
        release {
            //混淆代码
            minifyEnabled true
            debuggable false
            zipAlignEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "APlayer_v${defaultConfig.versionName}_${variant.productFlavors[0].name}.apk"
                }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    productFlavors {
//        tencent{}
//        alibaba{}
//        meizu{}

//        assistant91{}
//        baidu{}
//        anzhuomarket{}
//        qihu360{}
//        chuizi{}
//        xiaomi{}
        kuan{}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [CHANNEL_VALUE: name]
        }
    }
    lintOptions{
        checkReleaseBuilds false
        abortOnError false
        disable 'MissingTranslation'
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    configurations {
        all*.exclude group: 'com.squareup.okhttp3', module: 'okhttp'
        all*.exclude group: 'com.google.code.gson', module: 'gson'
    }

    configurations.all {
        resolutionStrategy.eachDependency { DependencyResolveDetails details ->
            def requested = details.requested
            if (requested.group == 'com.android.support') {
                if (!requested.name.startsWith("multidex")) {
                    details.useVersion "$supportLibVersion"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:palette-v7:$supportLibVersion"
    implementation "com.android.support:cardview-v7:$supportLibVersion"
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.facebook.fresco:fresco:1.5.0'
    implementation 'com.facebook.rebound:rebound:0.3.8'
    implementation 'com.umeng.analytics:analytics:6.1.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.soundcloud.android:android-crop:1.0.1@aar'
    implementation 'cn.bmob.android:bmob-sdk:3.5.0'
    implementation 'com.jakewharton:butterknife:8.8.1'
    implementation 'com.github.promeg:tinypinyin:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
}
